stripped_vdso = custom_target(
    'stripped-vdso',
    command: [strip, '@INPUT@', '-o@OUTPUT@'],
    input: vdso,
    output: 'stripped-vdso.so',
)
vdso_object = custom_target(
    'vdso-object',
    command: [objcopy, '@INPUT@', '@OUTPUT@', '-Ibinary', '-Oelf64-x86-64', '--rename-section=.data=.vdso'],
    input: stripped_vdso,
    output: 'vdso.o',
)

executable(
    'hydrogen',
    'src/cpu/cpu.c',
    'src/cpu/exc.c',
    'src/cpu/gdt.c',
    'src/cpu/gdt.S',
    'src/cpu/idt.c',
    'src/cpu/idt.S',
    'src/cpu/tss.c',
    'src/cpu/lapic.c',
    'src/cpu/lapic.S',
    'src/cpu/xsave.c',
    'src/drv/acpi.c',
    'src/drv/pic.c',
    'src/init/main.c',
    'src/init/main.S',
    'src/mem/obj/pmem.c',
    'src/mem/kmalloc.c',
    'src/mem/kvmm.c',
    'src/mem/pmap.c',
    'src/mem/pmm.c',
    'src/mem/vmalloc.c',
    'src/mem/vmm.c',
    'src/sys/syscall.c',
    'src/sys/syscall.S',
    'src/sys/usermem.c',
    'src/sys/usermem.S',
    'src/sys/vdso.c',
    'src/thread/mutex.c',
    'src/thread/sched.c',
    'src/thread/sched.S',
    'src/time/hpet.c',
    'src/time/kvmclock.c',
    'src/time/time.c',
    'src/time/tsc.c',
    'src/util/handle.c',
    'src/util/io.c',
    'src/util/logging.c',
    'src/util/object.c',
    'src/util/panic.c',
    'src/util/spinlock.c',
    c_args: [
        '-ffreestanding',
        '-fno-asynchronous-unwind-tables',
        '-fno-pie',
        '-fno-stack-check',
        '-fno-stack-protector',
        '-mcmodel=kernel',
        '-mgeneral-regs-only',
        '-mno-red-zone',
    ],
    implicit_include_directories: false,
    include_directories: [
        pub_inc,
        priv_inc,
        include_directories('include'),
    ],
    install: true,
    install_dir: '/boot',
    install_mode: 'rw-r--r--',
    link_args: ['-nostdlib', '-static', '-T' + (meson.current_source_dir() / 'kernel.lds')],
    link_depends: files('kernel.lds'),
    objects: vdso_object,
    sources: lib_sources,
)
